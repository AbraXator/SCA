plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

group = 'me.abraxator'
archivesBaseName = 'sca'

sourceSets {
    datagen {
        compileClasspath += main.output
    }
}
configurations {
    datagenImplementation.extendsFrom(minecraft)
}


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.19.4'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'sca'
            mods {
                sca {
                    source sourceSets.main

                }
            }
        }
        
        client {}
        server {}
        gameTestServer {}

        data {
            workingDirectory project.file('run')
            args '--mod', 'sca', '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/') 
            mods {
                sca {
                    source sourceSets.datagen
                }
            }

        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.0.49'
}

final manifestAttributes = [
        'Specification-Title'   : 'sca',
        'Specification-Vendor'  : 'AbraXator',
        'Specification-Version' : '1',
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'AbraXator'
]

jar {
    manifest.attributes(manifestAttributes)
    finalizedBy('reobfJar')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
